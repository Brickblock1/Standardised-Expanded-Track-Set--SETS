


spriteset(gfx_overlays_NBAN,			"gfx/NAAN/NBAN/Overlay_NBAN.png")		{	template_overlay_auz()				}
spriteset(gfx_underlays_NBAN,			"gfx/NAAN/NBAN/Underlay_NBAN.png")		{	template_underlay_auz()				}
spriteset(gfx_underlays_NBAN_wide,		"gfx/NAAN/NBAN/Underlay_NBAN_wide.png")	{	template_underlay_auz()				}
spriteset(gfx_tunnel_tracks_NBAN,		"gfx/NAAN/NBAN/TunnelTrack_NBAN.png")	{	template_tunneltracks()				}
spriteset(gfx_bridge_tracks_NBAN,		"gfx/NAAN/NBAN/BridgeTrack_NBAN.png")	{	template_bridgetracks()				}


spriteset(gfx_fences_NBAN,				"gfx/NAAN/NBAN/Fences_NBAN.png")		{	template_fences()					}
spriteset(gfx_depot_NBAN,				"gfx/NAAN/NBAN/Depot_NBAN.png")			{	template_depot()					}


spriteset(gfx_level_crossing_NBAN_empty,	"gfx/NAAN/NBAN/LevelCrossing_NBAN.png")	{tmpl_level_crossing_railtypes_empty()	}
spriteset(gfx_level_crossing_NBAN_closed,	"gfx/NAAN/NBAN/LevelCrossing_NBAN.png")	{template_levcrossclosed()				}
spriteset(gfx_level_crossing_NBAN_open,		"gfx/NAAN/NBAN/LevelCrossing_NBAN.png")	{template_levcrossopen()				}

spriteset(gfx_level_crossing_NBAN_empty_modern,		"gfx/NAAN/NBAN/LevelCrossingModern_NBAN.png")	{tmpl_level_crossing_railtypes_empty()	}
spriteset(gfx_level_crossing_NBAN_closed_modern,	"gfx/NAAN/NBAN/LevelCrossingModern_NBAN.png")	{template_levcrossclosed()				}
spriteset(gfx_level_crossing_NBAN_open_modern,		"gfx/NAAN/NBAN/LevelCrossingModern_NBAN.png")	{template_levcrossopen()				}


// Level crossing switches

switch(FEAT_RAILTYPES,	SELF,	sw_lc_NBAN_closed, current_year){ 
	0 .. 1970 - 1	: gfx_level_crossing_NBAN_closed ;
	gfx_level_crossing_NBAN_closed_modern;
}

switch(FEAT_RAILTYPES,	SELF,	sw_lc_NBAN_open, current_year){ 
	0 .. 1970 - 1	: gfx_level_crossing_NBAN_open ;
	gfx_level_crossing_NBAN_open_modern;
}

switch(FEAT_RAILTYPES, SELF, sw_lc_NBAN_state, level_crossing_status){
	LEVEL_CROSSING_CLOSED : sw_lc_NBAN_closed ;
	sw_lc_NBAN_open ;
}


// Wide ballast?

switch(FEAT_RAILTYPES, SELF, sw_underlay_NBAN, wide_ballast) {
 1: gfx_underlays_NBAN_wide;
 gfx_underlays_NBAN;
}


// Custom depot?

switch(FEAT_RAILTYPES, SELF, sw_depot_NBAN, depot_type){
	0 : gfx_depot_NBAN ;
	1 : gfx_depot_1 ;
}


// Fences?

switch(FEAT_RAILTYPES, SELF, sw_fences_NBAN, fence_number < 2){
	1 : gfx_fences_NBAN ;
	gfx_blank ;
}





// Item

item(FEAT_RAILTYPES, rail_NBAN) {
	property {
		label						: "NBAN" ;
		name						: string(STR_NBAN_NAME) ;
		menu_text					: string(STR_NBAN_NAME) ;
		build_window_caption		: string(STR_NAAN_BUILD_NAME) ;
		autoreplace_text			: string(STR_NAAN_REPLACE_NAME) ;
		new_engine_text				: string(STR_NAAN_NEW_TRAIN) ;
		toolbar_caption				: string(STR_NAAN_TOOLBAR) ;
		compatible_railtype_list	: [NARROW_GAUGE_RAILTYPE_ALL] ;
		powered_railtype_list		: [NARROW_GAUGE_RAILTYPE_ALL] ;
		curve_speed_multiplier		: 0 ;
		station_graphics			: RAILTYPE_STATION_NORMAL ;
		construction_cost			: 9 ;
		acceleration_model			: ACC_MODEL_RAIL ;
		railtype_flags				: bitmask() ;
		sort_order					: 102 ;
		speed_limit					: param[3] km/h ;
		extra_aspects				: signal_elec;
	}
	graphics {
		track_overlay		: gfx_overlays_NBAN ;
		underlay			: sw_underlay_NBAN ;
		bridge_surfaces		: gfx_bridge_tracks_NBAN ;
		tunnels				: gfx_tunnel_tracks_NBAN ;
		level_crossings		: sw_lc_NBAN_state ;
		fences				: sw_fences_NBAN ;
		depots				: sw_depot_NBAN ;
		signals				: sw_signals ;
	}
}

item(FEAT_RAILTYPES, rail_NBAE) {
	property {
		label						: "NBAE" ;
		name						: string(STR_NBAE_NAME) ;
		menu_text					: string(STR_NBAE_NAME) ;
		build_window_caption		: string(STR_NAAE_BUILD_NAME) ;
		autoreplace_text			: string(STR_NAAE_REPLACE_NAME) ;
		new_engine_text				: string(STR_NAAE_NEW_TRAIN) ;
		toolbar_caption				: string(STR_NAAE_TOOLBAR) ;
		compatible_railtype_list	: [NARROW_GAUGE_RAILTYPE_ALL] ;
		powered_railtype_list		: [NARROW_GAUGE_RAILTYPE_OHLE] ;
		curve_speed_multiplier		: 0 ;
		station_graphics			: RAILTYPE_STATION_NORMAL ;
		construction_cost			: 12 ;
		acceleration_model			: ACC_MODEL_RAIL ;
		railtype_flags				: bitmask(RAILTYPE_FLAG_CATENARY) ;
		sort_order					: 112 ;
		speed_limit					: param[3] km/h ;
		extra_aspects				: signal_elec;
	}
	graphics {
		track_overlay		: gfx_overlays_NBAN ;
		underlay			: sw_underlay_NBAN ;
		bridge_surfaces		: gfx_bridge_tracks_NBAN ;
		tunnels				: gfx_tunnel_tracks_NBAN ;
		level_crossings		: sw_lc_NBAN_state ;
		fences				: sw_fences_NBAN ;
		depots				: sw_depot_NBAN ;
		signals				: sw_signals ;
	}
}

if (custom_tunnels == 1) {
	item(FEAT_RAILTYPES, rail_NBAN) {
		graphics {
			tunnel_overlay	: gfx_blank ;
		}
	}
}

if (custom_tunnels == 1) {
	item(FEAT_RAILTYPES, rail_NBAE) {
		graphics {
			tunnel_overlay	: gfx_blank ;
		}
	}
}

if (hide_NAAE == 1) {
	item(FEAT_RAILTYPES, rail_NBAE) {
		property {
			railtype_flags	:bitmask(RAILTYPE_FLAG_CATENARY, RAILTYPE_FLAG_HIDDEN) ;
		}
	}
}